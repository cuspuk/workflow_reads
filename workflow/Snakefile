from snakemake.utils import min_version

min_version("7.25.0")


include: "rules/common.smk"


for step, tool in [item for item in config["reads"].items() if item[1] and not item[0].startswith("_")]:

    include: f"rules/{step}/{tool}.smk"


if config["reads"]["_generate_fastqc_for"]:

    include: "rules/fastqc.smk"


# Only include standalone rules if the module is used standalone, i.e. not as a part of a larger workflow
if not config.get("module_versions") or "reads" not in config["module_versions"]:

    include: "rules/standalone.smk"


wildcard_constraints:
    **get_constraints(),


rule all:
    input:
        **get_outputs(),
        **get_standalone_outputs(),
    localrule: True


if extra := get_all_relevant_extra_params():
    logger.info("Reads module - extra parameters parsed from config:")
    logger.info(get_all_relevant_extra_params())
